\begin{small}
\begin{Verbatim}[frame=single]
##
InstallMethod( SQVecObj,
               [ IsInt ],
               
  function( dim )
        local space;
        
        if dim < 0 then
            Error( "the given integer must be non-negative");
        fi;
        
        space := rec();
        
        ObjectifyObjectForCAPWithAttributes(
            space, SQVec,
            Dimension, dim
        );
        
        return space;
        
end );

##
InstallMethod( SQVecMor,
               [ IsSQVecObj, IsObject, IsSQVecObj ],
                  
  function( source, matrix, range )
    local morphism, underlying_matrix;

    if not IsHomalgMatrix( matrix ) then
    
      underlying_matrix := 
        HomalgMatrix( matrix,
                      Dimension( source ),
                      Dimension( range ),
                      QQ );

    else

      underlying_matrix := matrix;

    fi;
    
    morphism := rec();
    
    ObjectifyMorphismWithSourceAndRangeForCAPWithAttributes( 
      morphism, SQVec,
      source,
      range,
      UnderlyingMatrix, underlying_matrix
    );
    
    return morphism;
    
end );
\end{Verbatim}
\end{small}